Процедура ОткрытьСписокФайлов(Объект, ЭтаФорма) Экспорт

	Если Объект.ЭтоНовый() Тогда
		
		Если Вопрос("Предварительно документ должен быть записан. Записать документ?", РежимДиалогаВопрос.ДаНет) = 
			КодВозвратаДиалога.Да Тогда
		
			Попытка
			
				ЭтаФорма.ЗаписатьВФорме();
			
			Исключение
				
				Сообщить(ОписаниеОшибки());
				Возврат;
			
			КонецПопытки;
			
		Иначе
			
			Возврат;
		
		КонецЕсли;		
	
	КонецЕсли;
	
	ФормаСписка = Справочники.ХранилищеФайлов.ПолучитьФормуСписка(,ЭтаФорма);
	ФормаСписка.СправочникСписок.Отбор.Объект.Установить(Объект.Ссылка);
	ФормаСписка.ЭлементыФормы.СправочникСписок.Колонки.Объект.Видимость = Ложь;
	ФормаСписка.Открыть();

КонецПроцедуры

Процедура УстановитьКартинкуКнопкиОткрытияФайлов(КнопкаФайлы, Ссылка) Экспорт

	КоличествоФайлов = 0;
	
	Если Не Ссылка.Пустая() Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(ХранилищеФайлов.Ссылка) КАК КоличествоФайлов
			|ИЗ
			|	Справочник.ХранилищеФайлов КАК ХранилищеФайлов
			|ГДЕ
			|	ХранилищеФайлов.Объект = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			КоличествоФайлов = ВыборкаДетальныеЗаписи.КоличествоФайлов;
		Конецесли;
	
	КонецЕсли;

	Если КоличествоФайлов>0 Тогда
		
		КнопкаФайлы.Картинка = БиблиотекаКартинок.ЕстьФайлы;
		
	Иначе
		
		КнопкаФайлы.Картинка = БиблиотекаКартинок.НетФайлов;
		
	КонецЕсли;
	
	КнопкаФайлы.Текст = "Файлы ("+Формат(КоличествоФайлов,"ЧН=; ЧГ=")+")";
	
КонецПроцедуры

