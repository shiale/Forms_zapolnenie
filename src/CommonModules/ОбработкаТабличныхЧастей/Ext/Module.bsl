Процедура РасчетПоСтроке(ТекСтрока,ИзмениласьСумма = Ложь, Объект = Неопределено) Экспорт

	Если ИзмениласьСумма Тогда
		
		Если ТекСтрока.Количество<>0 Тогда
			ТекСтрока.Цена = ТекСтрока.Сумма/ТекСтрока.Количество;
		ИначеЕсли ТекСтрока.Цена<>0 Тогда
			ТекСтрока.Количество = ТекСтрока.Сумма/ТекСтрока.Цена;
		Иначе
			ТекСтрока.Количество = 1;
			ТекСтрока.Цена = ТекСтрока.Сумма;		
		КонецЕсли;
	Иначе
		ТекСтрока.Сумма = ТекСтрока.Количество*ТекСтрока.Цена;	
	КонецЕсли;

	Если Объект<>Неопределено Тогда
		
		Объект.РасчетСуммыДокумента();		
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЦенуПродажиТабЧасти(СтрокаТабличнойЧасти, ТипЦен, Дата = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) или Не ЗначениеЗаполнено(ТипЦен) Тогда
		
		Возврат;		
	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&Дата,
		|			ТипЦен = &ТипЦен
		|				И Номенклатура = &Номенклатура) КАК ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		СтрокаТабличнойЧасти.Цена = ВыборкаДетальныеЗаписи.Цена;
		
	Иначе	
		
		СтрокаТабличнойЧасти.Цена = 0;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьЦеныТабЧасти1(ТабЧасть, ТипЦен, Дата = Неопределено) Экспорт

	Если ТабЧасть.Количество()=0 Тогда
		
		Возврат;		
	
	КонецЕсли;	
	
	Для каждого Стр Из ТабЧасть Цикл
		
		ЗаполнитьЦенуПродажиТабЧасти(Стр, ТипЦен, Дата);
		РасчетПоСтроке(Стр);
	
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьЦеныТабЧасти2(ТабЧасть, ТипЦен, Дата = Неопределено) Экспорт

	Если ТабЧасть.Количество()=0 Тогда
		
		Возврат;		
	
	КонецЕсли;	
	
	мНоменклатура = ТабЧасть.ВыгрузитьКолонку("Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&Дата,
		|			ТипЦен = &ТипЦен
		|				И Номенклатура В (&мНоменклатура)) КАК ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("мНоменклатура", мНоменклатура);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЦеныНоменклатуры = Новый Соответствие;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЦеныНоменклатуры.Вставить(ВыборкаДетальныеЗаписи.Номенклатура,ВыборкаДетальныеЗаписи.Цена);
	КонецЦикла;
	
	Для каждого Стр Из ТабЧасть Цикл
		
		Стр.Цена = ЦеныНоменклатуры[Стр.Номенклатура];
		РасчетПоСтроке(стр);
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьЦеныТабЧасти3(ТабЧасть, ТипЦен, Дата = Неопределено) Экспорт

	Если ТабЧасть.Количество()=0 Тогда
		
		Возврат;		
	
	КонецЕсли;	
	
	ТЗ = ТабЧасть.Выгрузить(,"НомерСтроки,Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.НомерСтроки,
		|	ТЗ.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_ТЗ
		|ИЗ
		|	&ТЗ КАК ТЗ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена
		|ПОМЕСТИТЬ ВТ_Цены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&Дата,
		|			ТипЦен = &ТипЦен
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ.Номенклатура
		|					ИЗ
		|						ВТ_ТЗ КАК ВТ)) КАК ЦеныНоменклатурыСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТЗ.НомерСтроки,
		|	ВТ_ТЗ.Номенклатура,
		|	ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена
		|ИЗ
		|	ВТ_ТЗ КАК ВТ_ТЗ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		|		ПО ВТ_ТЗ.Номенклатура = ВТ_Цены.Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Стр = ТабЧасть[ВыборкаДетальныеЗаписи.НомерСтроки-1];
		Стр.Цена = ВыборкаДетальныеЗаписи.Цена;
		РасчетПоСтроке(Стр);
	КонецЦикла;
	
КонецПроцедуры














