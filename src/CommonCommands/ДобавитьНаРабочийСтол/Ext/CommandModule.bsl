
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ИмяФормы = ПараметрыВыполненияКоманды.Источник.ИмяФормы;
	
	Если ДобавитьНаСервере(ИмяФормы) Тогда
		
		ОбновитьИнтерфейс();		
	
	КонецЕсли;
	
	Окна = ПолучитьОкна();
	Для каждого Окно Из Окна Цикл
	
		Если Окно.НачальнаяСтраница Тогда
		
			Окно.Активизировать();
			Прервать;
		
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьНаСервере(ИмяФормы)

	НастройкиИзменены = Ложь;
	НастройкиНачальнойСтраницы = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиНачальнойСтраницы");
	
	Если НастройкиНачальнойСтраницы = Неопределено Тогда
		
		НастройкиНачальнойСтраницы = Новый НастройкиНачальнойСтраницы;
		СоставФорм = Новый СоставФормНачальнойСтраницы;
		СоставФорм.ЛеваяКолонка.Добавить(ИмяФормы);
		НастройкиИзменены = Истина;
		
	Иначе
		
		СоставФорм = НастройкиНачальнойСтраницы.ПолучитьСоставФорм();
		Если СоставФорм.ЛеваяКолонка.Найти(ИмяФормы)=Неопределено и
			 СоставФорм.ПраваяКолонка.Найти(ИмяФормы)=Неопределено Тогда
			 
			 Если СоставФорм.ЛеваяКолонка.Количество()>СоставФорм.ПраваяКолонка.Количество() Тогда
				 
				 СоставФорм.ПраваяКолонка.Добавить(ИмяФормы);
				 
			 Иначе
				 
				 СоставФорм.ЛеваяКолонка.Добавить(ИмяФормы);
			 
			 КонецЕсли;			
			 
			 НастройкиИзменены = Истина;
			 
		КонецЕсли;
	
	КонецЕсли;
	
	Если НастройкиИзменены Тогда
	
		НастройкиНачальнойСтраницы.УстановитьСоставФорм(СоставФорм);
		ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиНачальнойСтраницы", , НастройкиНачальнойСтраницы);
	
	КонецЕсли;

	Возврат НастройкиИзменены;
	
КонецФункции // ДобавитьНаСервере()








